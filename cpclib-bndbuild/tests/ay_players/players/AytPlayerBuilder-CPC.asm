;;============================================================================================================================================ 
;;              _____  __     _____  _____  _____    _____  _____  _____  _____    _____  __     _____  __ __  _____  _____
;;             |  |  ||  |   |_   _|| __  ||  _  |  |   __||  _  ||   __||_   _|  |  _  ||  |   |  _  ||  |  ||   __|| __  |
;;             |  |  ||  |__   | |  |    -||     |  |   __||     ||__   |  | |    |   __||  |__ |     ||_   _||   __||    -|
;;             |_____||_____|  |_|  |__|__||__|__|  |__|   |__|__||_____|  |_|    |__|   |_____||__|__|  |_|  |_____||__|__|
;;
;;                                                                                            ,,
;;                                 db      `YMM'   `MM'    MMP""MM""YMM                      *MM
;;                                ;MM:       VMA   ,V      P'   MM   `7                       MM
;;                               ,V^MM.       VMA ,V            MM      `7MM  `7MM  `7Mb,od8  MM,dMMb.   ,pW"Wq.
;;                              ,M  `MM        VMMP             MM        MM    MM    MM' "'  MM    `Mb 6W'   `Wb
;;                              AbmmmqMA        MM              MM        MM    MM    MM      MM     M8 8M     M8
;;                             A'     VML       MM              MM        MM    MM    MM      MM.   ,M9 YA.   ,A9
;;                           .AMA.   .AMMA.   .JMML.          .JMML.      `Mbod"YML..JMML.    P^YbmdP'   `Ybmd9'
;;
;;
;;============================================================================================================================================
;; 
;;                                                              _ (`-.         ('-.
;;                                                             ( (OO  )       ( OO ).-.
;;                                            ,----.          _.`     \       / . --. /
;;                                           '  .-./-')      (__...--''       | \-.  \
;;                                           |  |_( O- )      |  /  | |     .-'-'  |  |
;;                                           |  | .--, \      |  |_.' |      \| |_.'  |
;;                                          (|  | '. (_/      |  .___.'       |  .-.  |
;;                                           |  '--'  |  .-.  |  |      .-.   |  | |  | .-.
;;                                            `------'   `-'  `--'      `-'   `--' `--' `-'
;;
;;
;;                                                         .-') _      .-')                   .-')     .-') _       ('-.    _   .-')
;;                                                        ( OO ) )    ( OO ).                ( OO ).  (  OO) )    _(  OO)  ( '.( OO )_
;;   ,--.       .-'),-----.   ,----.      .-'),-----. ,--./ ,--,'    (_)---\_)   ,--.   ,--.(_)---\_) /     '._  (,------.  ,--.   ,--.)
;;   |  |.-')  ( OO'  .-.  ' '  .-./-')  ( OO'  .-.  '|   \ |  |\    /    _ |     \  `.'  / /    _ |  |'--...__)  |  .---'  |   `.'   |
;;   |  | OO ) /   |  | |  | |  |_( O- ) /   |  | |  ||    \|  | )   \  :` `.   .-')     /  \  :` `.  '--.  .--'  |  |      |         |
;;   |  |`-' | \_) |  |\|  | |  | .--, \ \_) |  |\|  ||  .     |/     '..`''.) (OO  \   /    '..`''.)    |  |    (|  '--.   |  |'.'|  |
;;   |  '---.'   \ |  | |  |(|  | '. (_/   \ |  | |  ||  |\    |     .-._)   \  |   /  /\_  .-._)   \    |  |     |  .--'   |  |   |  |
;;   |      |     `'  '-'  ' |  '--'  |     `'  '-'  '|  | \   |     \       /  `-./  /.__) \       /    |  |     |  `---.  |  |   |  |
;;   `------'       `-----'   `------'        `-----' `--'  `--'      `-----'     `--'       `-----'     `--'     `------'  `--'   `--'
;;
;;============================================================================================================================================
;; Mega Fucking Ultra Fucking Fast Player Fucking v2 for CPC- :-)
;; AYT Player generated by AYT Builder
;; AYT files generated by SeqMarker from YM files
;; CONSTANT CPU Execution, NO buffer, NO unpacking, LOOP everywhere, NO Address limitations,  NO ROM Flipping, TINY Size, INIT Ay included
;; NO DESTRUCTIVE Option
;;
;; 30th October 2025
;; Credits 
;; Delphi original AYT SeqMarker   
;; Web YM to AYT converter       : Tronic (GPA)
;; C Unix/Windows AYT converter  : Siko (Logon System) (Stephane Sikora @ https://www.sikorama.fr)
;; CPC AYT Z80A player/Builder   : Longshot (Logon System) (Serge Querne @ logon.system@free.fr)
;;
;;============================================================================================================================================
;; Performances
;; ------------
;; Option PlayerAccessByJP equ 0	; call player
;;     10 registers  : 369 nops (5.76 raster lines) / Player size : 252 bytes (*)
;;     11 registers  : 400 nops (6.25 raster lines) / Player size : 268 bytes (*)
;;     12 registers  : 431 nops (6.73 raster lines) / Player size : 286 bytes (*)
;;     13 registers  : 461 nops (7.20 raster lines) / Player size : 302 bytes (*)
;;     14 registers  : 490 nops (7.65 raster lines) / Player size : 317 bytes
;;     Size of Ayt_Builder : 480 bytes
;; (*) 56 last bytes of player are free again after 1st call of the player.
;;
;; Option PlayerAccessByJP equ 1	; jp player
;;     10 registers  : 358 nops (5sav.59 raster lines) / Player size : 247 bytes (*)
;;     11 registers  : 389 nops (6.07 raster lines) / Player size : 264 bytes (*)
;;     12 registers  : 420 nops (6.56 raster lines) / Player size : 281 bytes (*)
;;     13 registers  : 450 nops (7.03 raster lines) / Player size : 297 bytes (*)
;;     14 registers  : 479 nops (7.48 raster lines) / Player size : 312 bytes
;;     Size of Ayt_Builder : 466 bytes
;;
;; (*)With less than 13 registers, performance could vary by a few nops & bytes depending on the position of the constant registers.
;;--------------------------------------------------------------------------
;; Init AY Reg routine(only when register number < 14)
;;----------------------------------------------------
;; Size of routine : 57 bytes
;; The AY register initialization routine is created if constant registers require prior initialization. 
;; Note that if the initialization routine is not necessary, it will contain a single ret.
;;--------------------------------------------------------------------------
;; Notes :
;; Ayt Builder can be removed once music player built & music data initialized.
;; (i.e. you can use the player and the data file completely independently of the Ayt builder)
;; Performance is given for 10 to 14 non-constant registers, but the player can handle a variable number of constant registers. 
;; For example, if a song uses only two channels and nine active registers, then performance will be better.
;;============================================================================================================================================
;; Technical information about AYT Format
;; --------------------------------------
;; Ayt_Start
;; struct AYT_Header
;; { 
;;  uint8_t Ayt_Version      	; version aaaabbbb >> a.b
;;  uint16_t Ayt_ActiveRegs     ; active reg (bit 15:reg 0...bit 2:reg 13) 1=active 0:inactive
;;  uint8_t Ayt_PatternSize  	; Pattern size 1..255
;;  uint16_t Ayt_FirstSeqMarker ; Offset from Ayt_Start for Seq Ptr on patterns
;;  uint16_t Ayt_LoopSeqMarker 	; Offset from Ayt_Start for Loop Seq Ptr 
;;  uint16_t Ayt_ListInit	; Offset from Ayt_Start for Init of ay reg (*)
;;  uint16_t Ayt_NbPatternPtr  	; Nb of pattern ptr seq for music (NbSeq x NbReg)
;;  uint8_t  Ayt_PlatformFreq	; Platform (b0..4) & Frequency (b5..7) (see table) (**)
;;  uint8_t  Ayt_Reserved	; Reserved for future version (=0)
;; }
;; Start of Patterns
;;
;; (*)
;; struct Ay_Init 
;; {
;;  uint8_t Ayt_Reg		; No reg (0 to 13) 
;;  uint8_t Ayt_Val
;; } x N register to initialize
;; Last byte=#ff
;;
;; (**) 
;; bit 4..0 00000 AMSTRAD CPC (1 000 000 hz)
;;          00001 ORIC        (1 000 000 hz)
;;          00010 ZXUNO       (1 750 000 hz)
;;          00011 PENTAGON    (1 750 000 hz)
;;          00100 TIMEXTS2068 (1 764 000 hz)
;;          00101 ZX128       (1 773 450 hz)
;;          00110 MSX         (1 789 772 hz)
;;          00111 ATARI ST    (2 000 000 hz)
;;          01000 VG5000      (1 000 000 hz) (Ext card VG5210)
;;          11111 UNKNOWN  
;;
;; bit 7..5 000 50 hz
;;          001 25 hz
;;          010 60 hz
;;          011 30 hz
;;          100 100 hz
;;          101 200 hz
;;          111 UNKNOWN
;;
;; Start of Sequences of ptr on Pattern by Reg  
;;
;; A "Last Sequence" is generated after the last seq of AYT file.
;; Last sequence consists of Ayt_NbRegs pointers on 3 special 'pattern' bytes
;; This is done :
;; - to mute the music at end of play (some ym music don't do the job sometimes)
;; - to give end music status to player
;; Detail of the last sequence : Ayt_NbReg x WordPointer + 3 'pattern' bytes
;;               7 identical ptr on pattern 1 b with 0
;;               1 ptr for r7 on pattern 1b with 3f (mute ay)
;;               4/5 ptr for r8-r12/r13 on pattern 1 b with 0
;;		 1 ptr for r13 on pattern 1b 
;;			bit 7 >> if 1 : R13 not played or Music End
;;			bit 6 >> If 0 : If bit 7==1 then Music End Else r13 is not updated while playing std pattern 
;;                      bit 5 to 0 >> reserved (always 1)
;; i.e. this sequence points to these values (with Nbregs= 14) : 0,0,0,0,0,0,0,3F,0,0,0,0,0,BF
;;============================================================================================================================================
;;
;;============================================================================================================================================
;;============================================================================================================================================

;;----------------------------------------------------------------------------------------------------------------------------------------------
;; Compilation option
;;----------------------------------------------------------------------------------------------------------------------------------------------
;;
;; PlayerAccessByJP option
;;
;; In most situations, the player can be called with the Z80A "call" instruction. 
;; The player uses the stack as a fast data pointer. 
;; It therefore saves the SP register and returns it when it finishes its work.
;; However, in a few exceptional situations, you might need to modify SP in your main code (to also use it as a fast pointer), 
;; and it would therefore not be useful for the player to waste time saving it. 
;; This, however, requires calling the player differently. 
;; The "PlayerAccessByJP" compile option, when set to 1, allows you to assemble "Ayt_Builder" to handle access to the player via the Z80A "JP" instruction. 
;; It is then necessary to specify the return address in the input parameters of "Ayt_Builder" (in the HL register). 
;; Access to the player is then done with a "JP Player" instead of a "CALL Player", and the player will then return to your main program with a JP on the 
;; address you provided in HL to the "Ayt_builder".
;; In terms of technical figures, this option allows you to reduce the size of the built player by 5 bytes, and to save 11 nops of CPU 
;; (since the SP backup is no longer ensured) 
;; Also I draw your attention to the following point: this call method implies determining in advance the return address of the "player". 
;; In development, if the return address is a label that varies, this forces the presence of the Ayt_Builder in memory at each compilation.
;;
;;
ifndef PlayerAccessByJP
PlayerAccessByJP	equ 1		; If 1, requires you to take into account that SP has been wildly modified
endif
;;
;;----------------------------------------------------------------------------------------------------------------------------------------------
;;
;;----------------------------------------------------------------------------------------------------------------------------------------------
AYT_OFS_Version		equ 0	;; Not yet used (hey it's the 1st version)
AYT_OFS_ActiveRegs	equ 1	;; active reg (bit 0:reg 0...bit 13:reg 13) 1=active (only r12 off is managed in v1)
AYT_OFS_PatternSize	equ 3	;; Pattern size in bytes from 1 to 255
AYT_OFS_FirstSeq	equ 4	;; Offset on first Seq Ptr for start
AYT_OFS_LoopSeq		equ 6	;; Offset on Seq Ptr for loop
AYT_OFS_ListInit	equ 8	;; Offset on Ay Init List
AYT_OFS_NbPatternPtr	equ 10	;; Nb of pattern ptr seq for music (NbSeq x NbReg)
AYT_OFS_PlatformFreq	equ 12  ;; Platform & Freq of play (see table)
AYT_OFS_Reserved	equ 13	;; Rhaaaaaaa
AYT_SIZE_HEADER		equ 14	;; Header size to find first pattern

	ifnot PlayerAccessByJP
		OFS_B1_PtrSaveSP	equ Ayt_PtrSaveSP-Ayt_Player_B1_Start
		OFS_B3_Ayt_ReloadSP	equ Ayt_ReloadSP-Ayt_Player_B3_Start	
	endif
OFS_B1_FirstSeq		equ Ayt_FirstSeq-Ayt_Player_B1_Start 
OFS_B1_to_PatIdx	equ Ayt_PatternIdx-Ayt_FirstSeq

OFS_B3_SeqPatPtr_Upd	equ Ayt_SeqPatternPtr_Upd-Ayt_Player_B3_Start		
OFS_B3_PatCountPtr1	equ Ayt_PatCountPtr1-Ayt_Player_B3_Start
OFS_B3_PatCountPtr2	equ Ayt_PatCountPtr2-Ayt_Player_B3_Start			
OFS_B3_MusicCnt		equ Ayt_MusicCnt-Ayt_Player_B3_Start
OFS_B3_MusicCntPtr	equ Ayt_MusicCntPtr-Ayt_Player_B3_Start

AYT_B2_SIZE		equ Ayt_Player_B2_End-Ayt_Player_B2_Start	
AYT_B3_SIZE		equ Ayt_Player_B3_End-Ayt_Player_B3_Start
AYT_B4_SIZE		equ Ayt_Player_B4_End-Ayt_Player_B4_Start

_AYT_BUILDER_SIZE	equ AYT_Builder_End-AYT_Builder_Start
;-----------------------------------------------------------------------------------------------------------------------------------------------

;;**********************************************************************************************************************************************
;; Ayt Builder
;;
;;	in :	ix=address of AYT file
;;		de=address where Player is created
;;		a=nbloop expected
;;		hl (optional)=return address of player (only if Builder is compiled with PlayerAccessByJP equ 1)
;;	out : 
;;		de=ptr on the first free byte after the player
;;              hl=cpu (number of nops of the player including call method (jp or call))
;;
;;++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
AYT_Builder_Start
		di
		ld (Ayt_MusicCnt),a		; Nb of loop for music
		ld a,(ix+AYT_OFS_PatternSize)
		ld (Ayt_PatternSize),a		; Set Pattern Size
		push de				; Save ptr on player for init (if coded)
	if PlayerAccessByJP
			ld (Ayt_ExitPtr01),hl		; Set Main code return address
	else
			push de				; save ptr on first bloc (to restore player)
	endif	
		;;-------------------------------------------------------------------------------------------------------------------------------
		;; In AYT file , relocate sequence list ptr on rxx data. (absolute address)
		;; In >> ix=Ptr on AYT File
		;; Out >> de not modified
		;;-------------------------------------------------------------------------------------------------------------------------------
		push de				; save ptr for player
		push ix
		pop bc				; bc=Ptr on start of AYT file
		; 
		ld l,(ix+AYT_OFS_ListInit)	; Offset on Init List 
		ld h,(ix+AYT_OFS_ListInit+1)
		add hl,bc			; Ptr on Ay Init List 
		ld (Ayt_PtrInitList),hl		; In Block 4 if created
		ld a,(hl)			; 1st byte of list init
		ld (Ayt_InitCreate),a

		;
		ld l,(ix+AYT_OFS_LoopSeq)	; Offset of Loop Sequence
		ld h,(ix+AYT_OFS_LoopSeq+1)
		add hl,bc
		ld (Ayt_LoopSeq),hl		; Set Loop Sequence
		; 
		ld l,(ix+AYT_OFS_FirstSeq)	; read offset of first seq from start
		ld h,(ix+AYT_OFS_FirstSeq+1)
		add hl,bc			; Ptr on first sequence
		ld (Ayt_FirstSeq),hl
		push hl
		pop iy				; IY ptr on first sequence of pattern ptr by reg
		;
		ld hl,AYT_SIZE_HEADER
		add hl,bc
		ld b,h
		ld c,l
		;
		ld e,(ix+AYT_OFS_NbPatternPtr)
		ld d,(ix+AYT_OFS_NbPatternPtr+1); Nb of Pattern Ptr in all Sequence of AYT file
Ayt_PtrFix_b1
		ld l,(iy+0)
		ld h,(iy+1)			; Read Offset Seq
		add hl,bc			; Absolute Address for Seq Ptr on Rxx
		ld (iy+0),l
		ld (iy+1),h
		inc iy				; Next Seq Ptr rxx+1
		inc iy
		dec de
		ld a,d
		or e
		jr nz,Ayt_PtrFix_b1		; loop all sequence
		pop de
		;;-------------------------------------------------------------------------------------------------------------------------------
		;; Set ptr on var 1st bloc for later updates
		;;-------------------------------------------------------------------------------------------------------------------------------
		ld l,e
		ld h,d
		ld bc,OFS_B1_FirstSeq		; go to "ptr on first seq"
		add hl,bc
		push hl				; save Ptr on sequence ptr
		ld c,OFS_B1_to_PatIdx		; go to ptr index pattern 
		add hl,bc
		push hl				; save ptr of update of pattern idx
		;;
		;;-------------------------------------------------------------------------------------------------------------------------------
		;; To maintain init rout on constant time, needs to compute cpu for player
		;; init nop counter
		;;-------------------------------------------------------------------------------------------------------------------------------
		exx 
	if PlayerAccessByJP
			ld hl,21+56			; part of constant time player jp mode
	else
			ld hl,29+59			; part of constant time player call mode
	endif
		ld bc,31			; cpu in nop for send ay with inc c
		exx
		;;
		;;-------------------------------------------------------------------------------------------------------------------------------
		;; Searching first ay active reg 
		;;-------------------------------------------------------------------------------------------------------------------------------
		ld l,(ix+AYT_OFS_ActiveRegs)
		ld h,(ix+AYT_OFS_ActiveRegs+1)	; bit 15, 14, 13, ... of HL are active ay reg 0, 1, 2, 3... if 1
		xor a				; ay num 1st reg
Ayt_SearchActiveReg
		add hl,hl			; seek 1st non constant reg
		jr c,Ayt_ActiveReg_Found	; Active reg found				; 
		inc a				; not found , next reg
		cp 13
		jr nz,Ayt_SearchActiveReg
		jr $				; error in Ayt file >> no active reg in file
Ayt_ActiveReg_Found
		ld (Ayt_FirstAYReg),a		; Update first Ay reg to play in player block1
		;----------------------------------------------------------------
		; 1st reg updated then copy block 1 of player in ram
		;----------------------------------------------------------------
		;
		push hl				; Save active reg param
		ld hl,Ayt_PLayer_B1_Start
		ld bc,Ayt_Player_B1_End-Ayt_Player_B1_Start
		ldir
		pop hl	
		;			
		;----------------------------------------------------------------
		; Manage all active regs while creating player ay-send block
		;----------------------------------------------------------------
 		;
Ayt_CreateActiveReg
		push hl				; copy "send ay block" with "inc c"
		ld hl,Ayt_Player_B2_Start
		ld c,Ayt_Player_B2_End-Ayt_Player_B2_Start
		ldir
		exx
		add hl,bc			; nop counter Ay Send with "inc c"
		exx
		pop hl
		inc a				; ay reg counter
		add hl,hl			; seek next active reg
		jr c,Ayt_NextActReg		; next reg (+1) is active 
Ayt_SearchNextActReg				; current ay reg is not active
		cp 12				; is it the last one before r13?
		jr z,Ayt_Active_Reg		; yes, set 'ld c,12'
		inc a				; searching next active reg
		add hl,hl			; 
		jr nc,Ayt_SearchNextActReg	; search for ay reg active
Ayt_Active_Reg
		ex de,hl			; ay reg active found and not consecutive
		dec hl				; roll back last byte copied
		ld (hl),#0e			; coding [ld c,<regA>] instead of [inc c]
		inc hl
		ld (hl),a
		inc hl
		ex de,hl			; Current ptr for player restored
		exx
		inc hl				;; correct cpu : ld c,x instead of inc c
		exx
                jr nz,Ayt_CreateActiveReg	; not last register
		jr Ayt_LastReg
Ayt_NextActReg
		cp 13
		jr nz,Ayt_CreateActiveReg	; Create next "send ay bloc"
		dec de				; Last reg before r13 was r12, then inc c deleted (r13 manage "c" )
		exx
		dec hl				;; correct cpu : no ld c,x, no inc c
		exx
Ayt_LastReg
		;----------------------------------------------------------------
		; Create Last Part R13 & Loop control
		;----------------------------------------------------------------
		;
		ld hl,Ayt_Player_B3_Start
		ld c,AYT_B3_SIZE
		push de
		push de		
		ldir
		;
		;----------------------------------------------------------------
		; Link var
		;----------------------------------------------------------------
		;
		pop bc				; Ptr on struct B3 for calc
		pop iy				; Ptr on struct
		ld hl,OFS_B3_MusicCnt
		add hl,bc			; HL=Abs ptr on MusicCntPtr
		ld (iy+OFS_B3_MusicCntPtr),l
		ld (iy+OFS_B3_MusicCntPtr+1),h
		;
		pop hl				; HL=ptr on pattern idx ptr
		ld (iy+OFS_B3_PatCountPtr1),l
		ld (iy+OFS_B3_PatCountPtr1+1),h
		ld (iy+OFS_B3_PatCountPtr2),l
		ld (iy+OFS_B3_PatCountPtr2+1),h
		pop hl
		ld (iy+OFS_B3_SeqPatPtr_Upd),l	; HL=ptr on sequence ptr 
		ld (iy+OFS_B3_SeqPatPtr_Upd+1),h
		;
	ifnot PlayerAccessByJP				; Player called by call then reload SP
			ld hl,OFS_B3_Ayt_ReloadSP
			add hl,bc
			pop iy				; rec struct B1
			ld (iy+OFS_B1_PtrSaveSP),l
			ld (iy+OFS_B1_PtrSaveSP+1),h
	endif	
		;----------------------------------------------------------------
		; Last bloc built only if less than 14 registers
		;----------------------------------------------------------------
		pop hl				; Rec Ptr on player start
		
Ayt_InitCreate	equ $+1
		ld a,0				; Init Ay Reg at 1st call of player ?
		inc a				; Empty init list if #ff
		jr nz,Ayt_BuilderAlloc		; Yes
		exx
		push hl				; Rec cpu 
		exx
		pop hl				; Return HL=cpu, DE=last free ptr
		ret
Ayt_BuilderAlloc
		ld (Ayt_PlayerEntry),hl
		ld (hl),#c3			; jp init reg ay
		inc hl
		ld (hl),e			; replaced by cur ptr on player block4
		inc hl				; (not yet created)
		ld (hl),d
		;
		; Compute Cpu of Init Reg Ay Routine
		; 
		ld iy,(Ayt_PtrInitList)		; get ptr on ay reg to set
	if PlayerAccessByJP
			ld hl,83-35			; constant cpu for init jp mode
	else
			ld hl,82-35			; constant cpu for init call mode
	endif
		ld bc,35
Ayt_Init_CntReg
		bit 7,(iy+0)
		jr nz,Ayt_Exit_CntReg
		add hl,bc
		inc iy
		inc iy
		jr Ayt_Init_CntReg
Ayt_Exit_CntReg
		push hl				; hl=cpu of ay reg init
		exx
		pop de				; de=cpu of ay reg init
		push hl				; Save CPU player
		sbc hl,de			; hl=cpu of player >> cpu compensation
		ld a,l
		and %11
		xor %11
		ld (Ayt_FineWait),a
		srl h				; abs(hl/4) 4 nop gap
		rr l
		srl h
		rr l
		dec l				; lost 4 nop for fine wait
		ld a,l
		ld (Ayt_CptWait),a
		exx
		;
		ld hl,Ayt_Player_B4_Start
		ld bc,AYT_B4_SIZE		; block 4 created
		ldir				; DE=Ptr on last byte free
		pop hl				; Hl=Cpu player in nop
		ret
		
;===============================================================================================================================================
; Raw code template for the constructor
;===============================================================================================================================================
;-----------------------------------------------------------------------------------------------------------------------------------------------
Ayt_Player_B1_Start
		ld bc,#f680		; Port C + Data Reg AY 
	ifnot PlayerAccessByJP
Ayt_PtrSaveSP	equ $+2
		ld (0),sp		; 6 nop SP is saved if player is "called"
	endif		
Ayt_FirstSeq	equ $+1
		ld sp,0
		exx
Ayt_FirstAYReg	equ $+1
		ld bc,#f400		; Port A + No Reg AY
Ayt_PatternIdx	equ $+1
		ld de,0
		ld a,#e6		; Port C (printer on)
		out (#ff),a		; Select Ay Idx Reg (1st time)
Ayt_Player_B1_End
;-----------------------------------------------------------------------------------------------------------------------------------------------
Ayt_Player_B2_Start	
		out (c),c 		; Port A, select No Reg AY
		ld b,a			; Port C (F6)
		db #ed,#71		; out (c),#0 / Valid
		dec b			; F5>>F4 (port A) via OUTI
		pop hl			; Get Pattern Ptr
		add hl,de		; + offset in Pattern
		outi			; Send to Data Ay 
		exx
		out (c),c		; Port C, mode data
		out (#ff),a		; Port C, mode IdxReg+Valid
		exx
		inc c			; Num RegAY++
Ayt_Player_B2_End
;-----------------------------------------------------------------------------------------------------------------------------------------------
Ayt_Player_B3_Start
		pop hl			; Get Pattern Ptr for r13
		add hl,de		; + offset in Pattern
		bit 7,(hl)		; is it a valid reg ?
					; 47 nop left
		jr z,Ayt_SendAY_r13	; 3/2/2/2 
		bit 6,(hl)		; 0/3/3/3 is it the end of music
		jr nz,Ayt_NotLastSeq	; 0/3/2/2
Ayt_MusicCnt equ $+1
		ld a,0			; 0/0/2/2 loop counter 
		dec a			; 0/0/1/1
		jr z,Ayt_MusicEnd	; 0/0/3/2 loop finished ?
Ayt_MusicCntPtr equ $+1
		ld (0),a		; 0/0/0/4 no, loop counter upd
Ayt_LoopSeq  equ $+1
		ld sp,0			; 0/0/0/3 Ptr on sequence loop
		pop hl
		push hl
		inc hl
		dec hl			; 11 to wait
		jr Ayt_Player_NewSeq	; 0/0/0/3 >> 22+14=36 to 47
Ayt_MusicEnd				; Player on mute
					; 13
		ex (sp),hl		; 28 left
		ex (sp),hl
		ex (sp),hl
		ex (sp),hl
		inc hl
		dec hl	
		jr Ayt_PlayerExit	;  6 >> 19 to 47
		;------------------------
Ayt_NotLastSeq				;   8
		nop			; 
		ex (sp),hl		; 0/6/0
		ex (sp),hl		; 0/6/0 
		jr Ayt_Player_r13end	; 0/3/0 >> 24
Ayt_SendAY_r13				;(3)
		inc c			; 1 
		out (c),c		; 4  ; Port A, select No Reg AY
	        ld b,a			; 1  ; Port C (F6)
		db #ed,#71		; 4  ; out (c),#0 / Valid
	        dec b			; 1  ; F5
	        outi        		; 5  ; On Port A (F4) send (hl), hl++
	        exx        		; 1
	        out (c),c    		; 4 >>24 ; Port C, mode data
Ayt_Player_r13end
					;------------------
Ayt_PatCountPtr1 equ $+1
		ld a,(0)		; 4 Get Pattern offset
		inc a			; 1 Offset +1
Ayt_PatternSize	equ $+1
		cp  0			; 2 Pattern completed ?
		jr nz,Ayt_PatternCur	; 3/2 
Ayt_Player_NewSeq
Ayt_SeqPatternPtr_Upd equ $+2		; !!! Critical here !!!
		ld (0),sp		; 0/6 update ptr of seq on patterns
		xor a			; 0/1 offset all pattern to 0
Ayt_SeqPat
Ayt_PatCountPtr2 equ $+1		; 
		ld (0),a		; 0/4 update offset on patterns
Ayt_PlayerExit
	if PlayerAccessByJP
Ayt_ExitPtr01	equ $+1
		jp 0 			; 0/3 exit from player >> 23 nops
	else	
Ayt_ReloadSP	equ $+1
		ld sp,0			; 3 nop Player was called , SP is restored
		ret			; 3 and return to main code
	endif
Ayt_PatternCur				; 10+7=17 
		cp (hl)			; 2
		nop			; 1
		jr Ayt_SeqPat		; 3 >> 23 nops
Ayt_Player_B3_End
;-----------------------------------------------------------------------------------------------------------------------------------------------
; Init of non active AY reg
; 
Ayt_Player_B4_Start
		ld bc,#f680		; ppi port c mode data
		exx
		ld b,#f4		; ppi port a
		ld a,#e6		; Port C (printer on)
		out (#ff),a		; Select Ay Idx Reg (1st time)
Ayt_PtrInitList	equ $+1
		ld hl,0
Ayt_InitReg_Loop
		ld c,(hl)		; get register
		bit 7,c			; End list of init ay reg
		jr nz,Ayt_EndInitList	; yes
		inc hl			; no ptr on data
		out (c),c 		; Port A, select No Reg AY
		ld b,a			; Port C (F6)
		db #ed,#71		; out (c),#0 / Valid
		dec b			; F5>>F4 (port A) via OUTI
		outi			; Send to Data Ay 
		exx
		out (c),c		; Port C, mode data
		out (#ff),a		; Port C, mode IdxReg+Valid
		exx
		jr Ayt_InitReg_Loop	; next reg
Ayt_EndInitList
		exx
Ayt_PlayerEntry	equ $+1
		ld hl,0
		ld (hl),1		; restore ld bc,#f680 at player start addr
		inc hl
		ld (hl),c
		inc hl
		ld (hl),b
Ayt_FineWait	equ $+1			; jr 0,1,2,3 for fine cpu wait
		jr $
		nop
		nop
		nop
Ayt_CptWait	equ $+1			; wait by 4 nop steps
		ld a,0
Ayt_WaitForCoffee
		dec a
		jr nz,Ayt_WaitForCoffee
	if PlayerAccessByJP
			jr Ayt_PlayerExit	; End of init
	else
		ret
	endif
Ayt_Player_B4_End

Ayt_Builder_End
;;++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++



